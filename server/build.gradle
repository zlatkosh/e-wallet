plugins {
    id "org.springframework.boot" version "$springBootVersion"
    id("maven-publish")
    id("java-library")
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/zlatkosh/e-wallet"
            credentials {
                username System.getenv("GITHUB_ACTOR")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
        }
    }
}

archivesBaseName = "e-wallet"
dependencies {
    implementation project(':service')
    developmentOnly "org.springframework.boot:spring-boot-devtools:$springBootVersion"
}

afterEvaluate {
    List<String> deps = new ArrayList<>()
    configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        deps.add(artifact.name + "_" + artifact.moduleVersion.id.version)
    }
    springBoot {
        buildInfo {
            properties {
                artifact = 'e-wallet'
                additional = [
                        buildtime   : new Date().format('yyyyMMdd-HHmmss'),
                        dependencies: deps
                ]
            }
        }
    }
}